
.section .init
// macro
.macro SET_SP mode, sp_top
    mrs r0, cpsr
    orr r0, r0, #(1<<6 | 1<<7)   /* disable FIQ/IRQ */
    msr cpsr_c, r0

    /* Switch to mode */
    msr cpsr_c, \mode
    ldr sp, =\sp_top
    /* Switch back to SVC */
    msr cpsr_c, #0x13
.endm
.global _start
_start:
	/* Erkennen ob Hypervisor Mode aktiv */
	mrs r0, cpsr
	and r0, r0, #0x1F
	mov r1, #0x1A	//Modebits = 0x1A = Hypervisor Mode
	cmp r0, r1		//Vergleichen und Statusbits setzen für bedingte Sprünge

	/* Wenn Hypemode erkannt: abschalten (sprung nach _exitHyper)
	 * wenn nicht erkannt: dann weitermachen (weiterlaufen nach _bsprak)
	 */
	beq _exitHyper

	/* Qemu startet immer alle 4 Kerne
	 * Wir lassen alle anderen Kerne endless loopen
	 */
_checkCores:
	/* Id des Cpu Cores Abfragen */
	mrc p15, 0, r0, c0, c0, 5
	/* Falls nicht core 0 Core disablen */
	tst r0, #3
	bne _parkCore

/* not modeled in qemu 6.0 */
_enableAlignCheck:
	/* SCTLR des cp15 laden */
	mrc p15, 0, r0, c1, c0, 0
	/* A-bit (c1[1]) auf 1 setzen */
	orr r0, r0, #0x2
	/* neues SCTLR speichern */
	mcr p15, 0, r0, c1, c0, 0


_bsprak:
    /* 1) Set SP for current mode (SVC) first */
    ldr sp, =svc_stack_top

    /* 2) Initialize banked SPs for exceptions only */
    SET_SP 0x11, fiq_stack_top   /* FIQ */
    SET_SP 0x12, irq_stack_top   /* IRQ */
    SET_SP 0x17, abort_stack_top /* Abort */
    SET_SP 0x1B, und_stack_top   /* Undefined */

    /* 3) Set System/User SP safely in SVC mode */
    ldr r0, =sys_stack_top
    mov sp, r0

    /* 4) Jump to C kernel */
    bl start_kernel
.Lend:
	WFI
	b .Lend

_parkCore:
	/* Interrupts für Core 1 bis 3 ausschalten  */
	cpsid if
	/* In Endlosschleife springen */
	b .Lend

_exitHyper:

	/* Rücksprungadresse ins Hypervisor Rücksprungregister schreiben.
	 * Dies ist das Label _bsprak für die normale Programmausfürung.
	 */
	ldr lr, =_checkCores
	msr ELR_hyp, lr

	/* Der Hypervisor mode ist auch "nur" eine etwas kompliziertere Exception.
	 * Also springen wir aus dieser zurück.
	 * (Rücksprunge in Aufgabe 2 sind anders zu lösen!).
	 * Dazu zunächst das CPSR auslesen und die Modebits zum Supervisor ändern.
	 * Danach dies in das SPSR für den Rücksprung schreiben.
	 */
	mrs r0, cpsr
	bic r0, r0, #0x1F
	orr r0, r0, #0x13	//Supervisormode
	msr spsr_hyp, r0
	eret


